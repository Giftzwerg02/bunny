WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = ${ line_comment }
line_comment = { ";" ~ (!"\n" ~ ANY)* }

program = _{ SOI ~ func_call* ~ EOI }

expr = { 
	 float 
	 | int 
	 | string 
	 | array 
	 | map 
	 | func_def
	 | func_call 
	 | color 
	 | identifier
}

map = {
	"{" ~ map_assign* ~ "}"
}

map_assign = {
	expr ~ ":" ~ expr
}

array = {
	"[" ~ expr* ~ "]"
}

func_def = {
	"(" ~ "def" ~ identifier ~ parameters? ~ "(" ~ expr ~ ")" ~ ")"
}

parameters = {
	"(" ~ identifier* ~ ")"
}

func_call = {
	"(" ~ identifier ~ argument* ~ ")"
	| "(" ~ func_call* ~ ")"
}

argument = {
	(identifier ~ ":" ~ expr)
	| expr
}

identifier = @{
	(ASCII_ALPHA | special_char) ~ (ASCII_ALPHANUMERIC | special_char)*
}

special_char = @{
	"+" | "-" | "*" | "/" | "$" | "^" | "ยง" | "%" | "&" | "=" | "`" | "<" | ">" | "|" | "_" | "@" | "~"
}

int = @{ ASCII_DIGIT+ }
float = @{ ASCII_DIGIT+ ~ (("." ~ ASCII_DIGIT* ~ "f") | "f") }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
color = @{
	"#" ~ (ASCII_DIGIT | 'a'..'f' | 'A'..'F'){6}
}
